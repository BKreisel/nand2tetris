/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

    // Prep X
    And16(a=x, b=false, out=zeroX);
    Mux16(a=x, b=zeroX, sel=zx, out=pzx);
    Not16(in=pzx, out=notX);
    Mux16(a=pzx, b=notX, sel=nx, out=prepX);

    // Prep Y
    And16(a=y, b=false, out=zeroY);
    Mux16(a=y, b=zeroY, sel=zy, out=pzy);
    Not16(in=pzy, out=notY);
    Mux16(a=pzy, b=notY, sel=ny, out=prepY);

    // Compute
    Add16(a=prepX, b=prepY, out=cAdd);
    And16(a=prepX, b=prepY, out=cAnd);
    Mux16(a=cAnd, b=cAdd, sel=f, out=compute);

    // PostSet
    Not16(in=compute, out=notCompute);
    Mux16(
        a=compute,
        b=notCompute,
        sel=no,
        out[15]=ng,
        out[0..7]=lower8,
        out[8..15]=upper8,
        out=out
    );

    // ZR
    Or8Way(in=lower8, out=zr1);
    Or8Way(in=upper8, out=zr2);
    Or(a=zr1, b=zr2, out=zr3);
    Not(in=zr3, out=zr);
}